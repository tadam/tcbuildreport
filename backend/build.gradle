apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'

apply plugin: 'application'
mainClassName = 'com.github.tadam.tcbuildreport.backend.ServerKt'

apply plugin: 'com.bmuschko.docker-remote-api'
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

ext.ktor_version = '0.9.1'

sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions.jvmTarget = java_version
}
compileTestKotlin {
    kotlinOptions.jvmTarget = java_version
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5'
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-gson:$ktor_version"
    compile "io.ktor:ktor-locations:$ktor_version"
    compile "ch.qos.logback:logback-classic:1.2.1"
    compile 'org.redisson:redisson:3.6.3'
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.4.1"
    compile project(':teamcity-rest-client')

    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "io.ktor:ktor-server-test-host:$ktor_version"
}

shadowJar {
    baseName = 'backend-fat'
    classifier = null
    version = null
}

task buildImage(type: DockerBuildImage) {
    dependsOn shadowJar
    inputDir = file('.')
    tag = 'tudum/' + rootProject.name + '-' + project.name + ':' + rootProject.version
}

build.dependsOn buildImage